Use cWebHttpHandler.pkg
Use cCentrosDataDictionary.dd


Object oCentrosRestService is a cWebHttpHandler
 
    Object oCentros_DD is a cCentrosDataDictionary
    End_Object
    Set Main_DD to oCentros_DD

    Set psPath to "Api/Centros"
    Set psVerbs to "GET,POST,PATCH,DELETE"

    Procedure OnHttpGet String sPath String sAcceptType
        String[] aPath
        
        //  Split the path into parts
        Get StrSplitToArray sPath "/" to aPath
        If (SizeOfArray(aPath) = 0) Begin
               Send ListaCentros
            Procedure_Return
        End
        
        //  Redirect to a specific operation based on the path
        If (Lowercase(aPath[1]) <> "" and SizeOfArray(aPath) > 0 and Lowercase(aPath[1]) <> "centros") Begin
            If (SizeOfArray(aPath) > 1) Begin
                Send listarporid aPath[1]
            End
        End
        
            Else Begin
            Send SetResponseStatus 404 "Unknown resource" 
            End
            
           
    End_Procedure
    
    
    Procedure OnHttpPost String sPath String sContentType String sAcceptType Integer iSize
      String[] aPath
        
        //  Split the path into parts
        Get StrSplitToArray sPath "/" to aPath
        If (SizeOfArray(aPath) < 2) Begin
            Send SetResponseStatus 404 "Invalid resource" 
            Procedure_Return
        End
        
        //  Redirect to a specific operation based on the path
        If (Lowercase(aPath[1]) = "centros") Begin
            If (SizeOfArray(aPath) > 2 and aPath[2] <> "") Begin
                Send SetResponseStatus 400 "Only use POST for creating new resources"
            End
            Else Begin
                Send CrearCentro True ""
            End
        End
        Else Begin
            Send SetResponseStatus 404 "Unknown resource"
        End
    
    End_Procedure
    
    
    Procedure OnHttpPatch String sPath String sContentType Integer iSize
        String[] aPath
        
        //  Split the path into parts
        Get StrSplitToArray sPath "/" to aPath
        If (SizeOfArray(aPath) < 2) Begin
            Send SetResponseStatus 404 "Invalid resource" 
            Procedure_Return
        End
        
        //  Redirect to a specific operation based on the path
        If (Lowercase(aPath[1]) = "centros") Begin
            If (SizeOfArray(aPath) > 2 and aPath[2] <> "") Begin
                Send CrearCentro False aPath[2]
            End
            Else Begin                
                Send SetResponseStatus 400 "Only use PATCH for updating existing  resources"
            End
        End
        Else Begin
            Send SetResponseStatus 404 "Unknown resource"
        End
    End_Procedure
    
    
    Procedure OnHttpDelete String sPath
        String[] aPath
        
        Get StrSplitToArray sPath "/" to aPath
        If (SizeOfArray(aPath) < 2) Begin
            Send SetResponseStatus 404 "Invalid resource" 
            Procedure_Return
        End
        
        If (Lowercase(aPath[1]) = "centros") Begin
            If (SizeOfArray(aPath) > 2 and aPath[2] <> "") Begin
                Send Deletecentros aPath[2]
            End
            Else Begin                
                Send SetResponseStatus 400 "Only use DELETE on existing resources"
            End
        End
        Else Begin
            Send SetResponseStatus 404 "Unknown resource"
        End
    End_Procedure
    

    Procedure ListaCentros
        Handle hoJson hoCentro

        Get Create (RefClass(cJsonObject)) to hoJson
        Send InitializeJsonType of hoJson jsonTypeArray

        Get Create (RefClass(cJsonObject)) to hoCentro

        Send Find of oCentros_DD FIRST_RECORD 1
        While (Found)
            Send InitializeJsonType of hoCentro jsonTypeObject

            Send SetMemberValue of hoCentro "Id" jsonTypeInteger Centros.Id
            Send SetMemberValue of hoCentro "Nombre" jsonTypeString Centros.Nombre
            Send SetMemberValue of hoCentro "PsNombre" jsonTypeString Centros.PsNombre
            Send AddMember of hoJson hoCentro

            Send Find of oCentros_DD NEXT_RECORD 1
        Loop

        Send OutputJson hoJson

        Send Destroy of hoCentro
        Send Destroy of hoJson
    End_Procedure

    Procedure OutputJson Handle hoJson
        UChar[] ucData

        Send AddHttpResponseHeader "Content-Type" "application/json"
        Get StringifyUtf8 of hoJson to ucData
        Send OutputUChar ucData
    End_Procedure
    
    Procedure listarporid Integer iId
        Handle hoJson
        
        Send Clear of oCentros_DD
        Move iId to Centros.Id
        Send Find of oCentros_DD EQ 1
        
        If (Found) Begin
            Get Create (RefClass(cJsonObject)) to hoJson
            Send InitializeJsonType of hoJson jsonTypeObject
            
             Send SetMemberValue of hoJson "Id" jsonTypeInteger Centros.Id
             Send SetMemberValue of hoJson "Nombre" jsonTypeString Centros.Nombre
             Send SetMemberValue of hoJson "PsNombre" jsonTypeString Centros.PsNombre
             Send SetMemberValue of hoJson "Direccion" jsonTypeString Centros.Direccion
             Send SetMemberValue of hoJson "Pais" jsonTypeString Centros.Pais
             Send SetMemberValue of hoJson "ComunidadAutonoma" jsonTypeString Centros.ComunidadAutonoma
             Send SetMemberValue of hoJson "Localidad" jsonTypeString Centros.Localidad
             Send SetMemberValue of hoJson "CodigoPostal" jsonTypeInteger Centros.CodigoPostal
             Send SetMemberValue of hoJson "Empresa" jsonTypeString Centros.Empresa
//             Send SetMemberValue of hoJson "FechaCreacionRegistro" jsonTypeString Centros.FechaCreacionRegistro
//             Send SetMemberValue of hoJson "FechaUltimaModificacion" jsonTypeString Centros.FechaUltimaModificacion
            
            Send OutputJson hoJson
            Send Destroy of hoJson
        End
        Else Begin
            Send SetResponseStatus 403 "Resource not found"
        End
    End_Procedure
    
    
    Procedure CrearCentro Boolean bCreate String sKey
        Handle hoJson
        Boolean bSuccess
        String sVal
        Boolean bVal bErr
        
        //  Find the right record
        If (bCreate) Begin
            Send Clear of oCentros_DD
        End
        Else Begin
            Send Clear of oCentros_DD
            Move sKey to Centros.Id
            Send Find of oCentros_DD EQ 1
            
            If (not(Found)) Begin
                Send SetResponseStatus 404 "Resource not found"
                Procedure_Return
            End
        End
        
        //  Read the request data
        Get RequestDataJson (&hoJson) to bSuccess
        
        If (bSuccess) Begin
            Send ErrorQueueStart
            
            //  Set the values
            If (HasMember(hoJson, "Nombre")) Begin
                Get MemberValue of hoJson "Nombre" to sVal
                Set Field_Changed_Value of oCentros_DD (RefTable(Centros.Nombre)) to sVal
            End
            
            If (HasMember(hoJson, "PsNombre")) Begin
                Get MemberValue of hoJson "PsNombre" to sVal
                Set Field_Changed_Value of oCentros_DD (RefTable(Centros.PsNombre)) to sVal
            End
            
            
            If (HasMember(hoJson, "Direccion")) Begin
                Get MemberValue of hoJson "Direccion" to sVal
                Set Field_Changed_Value of oCentros_DD (RefTable(Centros.Direccion)) to sVal
            End
            
            If (HasMember(hoJson, "Pais")) Begin
                Get MemberValue of hoJson "Pais" to sVal
                Set Field_Changed_Value of oCentros_DD (RefTable(Centros.Pais)) to sVal
            End
            
            If (HasMember(hoJson, "ComunidadAutonoma")) Begin
                Get MemberValue of hoJson "ComunidadAutonoma" to sVal
                Set Field_Changed_Value of oCentros_DD (RefTable(Centros.ComunidadAutonoma)) to sVal
            End
            
            If (HasMember(hoJson, "Localidad")) Begin
                Get MemberValue of hoJson "Localidad" to sVal
                Set Field_Changed_Value of oCentros_DD (RefTable(Centros.Localidad)) to sVal
            End
            
            If (HasMember(hoJson, "CodigoPostal")) Begin
                Get MemberValue of hoJson "CodigoPostal" to sVal
                Set Field_Changed_Value of oCentros_DD (RefTable(Centros.CodigoPostal)) to sVal
            End
            
            If (HasMember(hoJson, "Empresa")) Begin
                Get MemberValue of hoJson "Empresa" to sVal
                Set Field_Changed_Value of oCentros_DD (RefTable(Centros.Empresa)) to sVal
            End
            
            //  Perform the save
            If (ErrorCount(Self) = 0) Begin
                Get Request_Validate of oCentros_DD to bErr
                If (not(bErr)) Begin
                   Send Request_Save of oCentros_DD     
                   
                   If (not(Err)) Begin
                        Send listarporid Centros.Id
                   End
                End
            End            
            
            Send ErrorQueueEnd
            Send ErrorReportJson
        End
        
        If (hoJson > 0) Begin
            Send Destroy of hoJson
        End
    End_Procedure
    
        //  Deletes a specific customer record
    Procedure DeleteCentros String sKey
        Send Clear of oCentros_DD
        Move sKey to Centros.Id
        Send Find of oCentros_DD EQ 1
        
        If (not(Found)) Begin
            Send SetResponseStatus 404 "Resource not found"
            Procedure_Return
        End
        
        Send ErrorQueueStart
        
        Send Request_Delete of oCentros_DD
        
        Send ErrorQueueEnd
        Send ErrorReportJson
    End_Procedure
    
    
        //  Helper function that reads an entire request into a JSON object.
    Function RequestDataJson Handle ByRef hoJson Returns Boolean
        UChar[] ucData
        Boolean bSuccess
        String sErr
        
        Get Create (RefClass(cJsonObject)) to hoJson
        
        Get RequestDataUChar -1 to ucData
        Get ParseUtf8 of hoJson ucData to bSuccess
        
        If (not(bSuccess)) Begin
            Get psParseError of hoJson to sErr
            Send SetResponseStatus 400 sErr
        End
        
        Function_Return bSuccess
    End_Function
    
    
      Procedure ErrorReportJson
        Integer iErr iErrC
        Handle hoJson hoErrors   
        String sMessage
        
        Get ErrorCount to iErrC
        If (iErrC > 0) Begin
            Send SetResponseStatus 400 "Errors occurred"
            
            Get Create (RefClass(cJsonObject)) to hoJson
            Send InitializeJsonType of hoJson jsonTypeObject
            
            Get Create (RefClass(cJsonObject)) to hoErrors
            Send InitializeJsonType of hoErrors jsonTypeArray
            
            For iErr from 0 to (iErrC - 1)
                Get ErrorMessage iErr to sMessage
                Send AddMemberValue of hoErrors jsonTypeString  sMessage
            Loop
            
            Send SetMember of hoJson "aErrors" hoErrors
            
            Send OutputJson hoJson
            
            Send Destroy of hoErrors
            Send Destroy of hoJson
        End
    End_Procedure
    

End_Object